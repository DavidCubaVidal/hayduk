DECLARE @MODULO_SAP VARCHAR(75) = 'PM', @TABLA_STAGE VARCHAR(75) = 'ESTADO_OBJETO', @HORARIO VARCHAR(75) = 'MANANA', @LOG_ID INT, @MENSAJE VARCHAR(MAX);

BEGIN TRY
	
	-- LOG INI
	INSERT INTO CARGA_STAGE_LOG VALUES (@MODULO_SAP, 'POST_SCRIPT', @TABLA_STAGE, CAST(GETDATE()AS DATE), @HORARIO, GETDATE(), NULL, 0, 0, 'Inicio Carga', 0);
	SET @LOG_ID = @@IDENTITY;

	-- Carga la tabla en Stage
	IF OBJECT_ID('ESTADO_OBJETO', 'U') IS NOT NULL
	DROP TABLE ESTADO_OBJETO;

	SELECT DISTINCT 
	ROW_NUMBER() OVER(PARTITION BY a1.OBJNR ORDER BY ISNULL(z3.LINEP,'99'),a2.TXT04 ASC) AS NUM,
	a1.MANDT,
	RTRIM(LTRIM(a1.OBJNR))AS OBJNR, 
	(SELECT top 1 a2.TXT04 from HDKSAPSQL.PRD.prd.TJ02T a2 (nolock) WHERE a1.STAT = a2.ISTAT AND a2.SPRAS in ('S'))AS STAT,
	ISNULL(z3.LINEP,'99') as LINEP
	INTO ESTADO_OBJETO
	FROM HDKSAPSQL.PRD.prd.JEST a1 (nolock)
	INNER JOIN HDKSAPSQL.PRD.prd.TJ02 z (nolock) ON a1.STAT = z.ISTAT 
	INNER JOIN HDKSAPSQL.PRD.prd.TJ02T a2 (nolock) ON a1.STAT = a2.ISTAT AND a2.SPRAS in ('S')
	INNER JOIN HDKSAPSQL.PRD.prd.JSTO z2 (nolock) ON a1.MANDT = z2.MANDT AND a1.OBJNR = z2.OBJNR
	LEFT OUTER JOIN HDKSAPSQL.PRD.prd.TJ04 z3 (nolock) ON z2.OBTYP = z3.OBTYP AND a1.STAT = z3.ISTAT 
	WHERE a1.MANDT = '300' 
	AND a1.INACT <> 'X'
	AND z.NODIS <> 'X';

	ALTER TABLE ESTADO_OBJETO ADD STAT_F varchar(250) DEFAULT NULL;

	IF OBJECT_ID('tempdb..#ESTADO_OBJETO1') IS NOT NULL
	DROP TABLE #ESTADO_OBJETO1;

	SELECT OBJNR,
	RTRIM(LTRIM(
	isnull([1],'') + ' ' + isnull([2],'') + ' ' + isnull([3],'') + ' ' + isnull([4],'') + ' ' + isnull([5],'') + ' ' +
	isnull([6],'') + ' ' + isnull([7],'') + ' ' + isnull([8],'') + ' ' + isnull([9],'') + ' ' + isnull([10],'')
	)) AS Estado
	INTO #ESTADO_OBJETO1
	FROM
	(
	SELECT NUM, OBJNR, STAT
	FROM ESTADO_OBJETO
	WHERE LEFT(RTRIM(LTRIM(OBJNR)),2) = 'IE'
	) AS TablaFuente
	PIVOT
	(
	MIN(STAT) FOR NUM in ([1],[2],[3],[4],[5],[6],[7],[8],[9],[10])
	) AS TablaPivot

	UPDATE A
	SET STAT_F = B.Estado
	FROM ESTADO_OBJETO A
	INNER JOIN #ESTADO_OBJETO1 B on A.OBJNR = B.OBJNR 
	WHERE LEFT(RTRIM(LTRIM(A.OBJNR)),2) = 'IE'

	-- LOG FIN
	UPDATE CARGA_STAGE_LOG SET FECHA_FIN = GETDATE(), DURACION = DATEDIFF(SECOND,FECHA_INI,GETDATE()), REGISTROS = @@ROWCOUNT, EXITO = 1, MENSAJE = 'Carga completa' WHERE LOG_ID = @LOG_ID;

END TRY
BEGIN CATCH

	SET @MENSAJE = ERROR_MESSAGE()
	UPDATE CARGA_STAGE_LOG SET FECHA_FIN = GETDATE(), REGISTROS = @@ROWCOUNT, EXITO = 0, MENSAJE = @MENSAJE WHERE LOG_ID = @LOG_ID;
	RETURN

END CATCH
